buildscript {
	ext {
		snippetsDir = file('build/generated-snippets')
		springCloudVersion = "Greenwich.RELEASE"
		springBootVersion = '2.1.3.RELEASE'
	}

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springBootVersion}"
	}
}

plugins {
	id "org.asciidoctor.convert" version "1.5.3"
	id 'java'
	id 'groovy'
	id 'com.palantir.docker' version '0.22.1'
	id "org.sonarqube" version "2.7.1"
	id 'jacoco'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'spring-cloud-contract'
apply plugin: 'org.springframework.boot'

group = 'com.kyripay'
version = project.hasProperty('customVersion') ? project.customVersion : '0.1.2'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springBootVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka'
	testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.cloud:spring-cloud-starter-contract-verifier'

	asciidoctor    'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testCompile    'org.springframework.restdocs:spring-restdocs-restassured'

	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'

	testCompile 'io.rest-assured:rest-assured:3.3.0'
	testCompile "org.testcontainers:testcontainers:1.12.0"
	testCompile "org.testcontainers:kafka:1.12.0"
	testCompile "com.lordofthejars:nosqlunit-mongodb:1.0.0"
	testCompile "com.github.fakemongo:fongo:2.1.0"
	testCompile "org.skyscreamer:jsonassert:1.5.0"
}


test {
	include '**/unitTests/**'
}

task integrationTests(type: Test) {
	include '**/integrationTests/**'
}

task contractTests(type: Test) {
	include '**/contractTests/**'
}

task apiTests(type: Test) {
	outputs.dir snippetsDir
	include '**/apiTests/**'
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn apiTests
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

contracts{
	baseClassForTests = "com.kyripay.converter.contractTests.ContractTestBase"
}


docker {
	dependsOn build
	name "kyripay-converter"
	tag "version", "kyripay/converter:${project.version}"
	tag "latest", "kyripay/converter:latest"
	files tasks.bootJar.outputs
	buildArgs([VERSION: project.version])
}

sonarqube {
	properties {
		property "sonar.projectKey", "kyripay-converter"
		property "sonar.organization", "kyripay"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.login", "141234b42c830a342a07bae592c494d55567b4af"
	}
}

jacocoTestReport {
	executionData "$buildDir/jacoco/apiTests.exec",
			"$buildDir/jacoco/integrationTests.exec",
			"$buildDir/jacoco/contractTests.exec",
			"$buildDir/jacoco/apiTests.exec"
	reports {
		xml.enabled true
	}
}